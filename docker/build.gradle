import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerPushImage

plugins {
    id 'com.bmuschko.docker-remote-api' version '5.3.0'
}

def dockerBuildDir = file("${project.buildDir}/docker")
List tagsToBuild = (
        (System.getenv("CI_COMMIT_REF_NAME").equals("master") ? ["devel"] : []) +
                ((System.getenv("CI_COMMIT_TAG") ?: "").startsWith("release/") ? System.getenv("CI_COMMIT_TAG").substring("release/".length()) : [])
).collect { "dajudge/kafkaproxy:$it" }
if (!tagsToBuild.isEmpty()) {
    println "Building docker images: $tagsToBuild"
}

task clean(type: Delete) {
    group "build"
    delete "${project.buildDir}"
}

task copyJars(type: Copy) {
    group "build"
    [":app", ":ca-selfsign"].each {
        dependsOn project(it).tasks.withType(Jar)
        from project(it).tasks.withType(Jar)
        from project(it).configurations.runtimeClasspath
    }
    into dockerBuildDir
}

task copyDockerSources(type: Copy) {
    group "build"
    from file("src/main/docker")
    into dockerBuildDir
}

task buildAppImage(type: DockerBuildImage) {
    group "build"
    dependsOn copyJars, copyDockerSources
    inputDir = dockerBuildDir
    tags.addAll(tagsToBuild)
}

task build {
    group "build"
    dependsOn buildAppImage
}

task publish {
    group "publish"
    dependsOn build
}

if (!tagsToBuild.isEmpty()) {
    def taskName = { String str -> str.replaceAll("[/:]", "-") }
    tagsToBuild.each { tag ->
        publish.dependsOn task([type: DockerPushImage], "push_${taskName(tag)}", {
            group "publish"
            dependsOn buildAppImage
            imageName = tag
            registryCredentials.username = System.getenv("DOCKERHUB_USERNAME")
            registryCredentials.password = System.getenv("DOCKERHUB_PASSWORD")
        })
    }
}
