plugins {
    id "com.github.hierynomus.license" version "0.15.0" apply false
}

ext {
    kafkaClientVersion = "2.5.0"
    proxyBaseVersion = "0.0.13"
    libKafkaClient = "org.apache.kafka:kafka-clients:$kafkaClientVersion"
    libProxyBase = "com.dajudge.proxybase:proxybase:$proxyBaseVersion"
    libProxyBaseTestca = "com.dajudge.proxybase:testca:$proxyBaseVersion"
}

subprojects {
    repositories {
        mavenLocal()
        maven { url "https://dl.bintray.com/dajudge/proxybase" }
        mavenCentral()
    }
    tasks.withType(JavaCompile) {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
}

wrapper {
    version "5.3.1"
}

allprojects {
    apply plugin: 'com.github.hierynomus.license'
    apply plugin: 'pmd'
    apply plugin: 'findbugs'

    findbugs {
        toolVersion = "3.0.1"
    }

    tasks.withType(FindBugs) {
        reports {
            xml.enabled false
            html.enabled true
        }
    }

    pmd {
        ruleSets = [project.rootProject.file("pmd.xml").absolutePath]
    }

    license {
        header file("${project.rootDir}/HEADER")
    }
}
